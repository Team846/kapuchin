buildscript {
    repositories {
        gradlePluginPortal()
        jcenter()
        maven { url 'http://repo.kunalsheth.info/' }
        mavenLocal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "info.kunalsheth.units:plugin:$uoms_version"
        classpath "edu.wpi.first:GradleRIO:$gradlerio_version"
    }
}

allprojects {

    group 'com.lynbrookrobotics'
    version '1.0.0'

    repositories {
        jcenter()
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '11'
            freeCompilerArgs += [
                    '-Xinline-classes',
                    '-Xopt-in=kotlin.ExperimentalUnsignedTypes'
            ]
        }
    }

    // https://guides.gradle.org/performance/#parallel_test_execution
    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}