import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

apply plugin: 'kotlin-platform-jvm'
apply plugin: 'jaci.openrio.gradle.GradleRIO'
apply plugin: 'jacoco'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'

    expectedBy project(":architecture")
    compile wpilib()
    compile ctre()
    compile 'com.github.juchong:ADIS16448-RoboRIO-Driver:2018.3'

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

repositories.maven { url 'https://jitpack.io' }

deploy {
    targets { target('roborio', RoboRIO) { team = 846 } }
    artifacts {
        artifact(rootProject.name, FRCJavaArtifact) {
            targets << 'roborio'
//            jvmArgs << '-verbose:class'
//            arguments << 'myCustomArgs'
//            debug = true
//            debugPort = 8348
        }
    }
}

wpi {
    shuffleboardVersion = '1.3.1'
}

group 'com.lynbrookrobotics'
version '1.0.0'

compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }
kotlin.experimental.coroutines "enable"

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest('com.lynbrookrobotics.kapuchin.FunkyRobot')
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
check.dependsOn jacocoTestReport