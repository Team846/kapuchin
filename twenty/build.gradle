import edu.wpi.first.gradlerio.GradleRIOPlugin

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'edu.wpi.first.GradleRIO'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    implementation project(path: ':architecture', configuration: 'jvmfrcDefault')

    implementation wpi.deps.wpilib()
    implementation wpi.deps.vendor.java()
    nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

deploy {
    targets {
        roboRIO('roborio') {
            team = 846
        }
    }

    artifacts {
        frcJavaArtifact(project.name) {
            targets << 'roborio'
            jvmArgs /*<< '-XX:+UseG1GC'*/ << '-XX:MaxGCPauseMillis=75' << '-Xmx100M'
//          jvmArgs << '-verbose:class'
//          jvmArgs << '-verbosegc'

            // may need to run this on roborio
            // echo "127.0.0.1 $HOSTNAME" | sudo tee -a /etc/hosts
            jvmArgs << '-Dcom.sun.management.jmxremote=true'
            jvmArgs << '-Dcom.sun.management.jmxremote.port=1099'
            jvmArgs << '-Dcom.sun.management.jmxremote.local.only=false'
            jvmArgs << '-Dcom.sun.management.jmxremote.ssl=false'
            jvmArgs << '-Dcom.sun.management.jmxremote.authenticate=false'
            jvmArgs << '-Djava.rmi.server.hostname=roborio-846-frc.local'
        }
    }
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest GradleRIOPlugin.javaManifest('com.lynbrookrobotics.twenty.FunkyRobotKt')
}

def buildInfoDir = "build/buildInfo/"

task generateBuildInfo {
    doLast {
        mkdir buildInfoDir

        def git = { args -> "git $args".execute().text.trim() }
        def info = [
                'DateTime'        : new Date().format('MM/dd/yyyy HH:mm:ss'),
                'User'            : git('config user.name'),
                'GitHasUncommited': git('status --porcelain').isBlank() ? 'No' : 'Yes',
                'GitBranch'       : git('branch --show-current'),
                'GitHash'         : git('rev-parse HEAD')
        ]

        info.each { key, value -> file("$buildInfoDir${key}.txt").text = value }
    }
}

// Run after compileKotlin and before processResources
processResources.dependsOn generateBuildInfo
generateBuildInfo.mustRunAfter compileKotlin
sourceSets.main.resources.srcDir buildInfoDir
